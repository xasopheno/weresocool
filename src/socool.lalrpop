use std::str::FromStr;
use crate::ast::{Op};

grammar;

Comma<T>: Vec<T> = { // (0)
    <v:(<T> ",")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};



pub Operation: Op = {
    "AsIs" => Op::AsIs,
    "Reverse" => Op::Reverse,
    "Silence" <v:Value> => Op::Silence {m: v},
    "Tm" <v:Value> => Op::TransposeM {m: v},
    "Ta" <v:Value> => Op::TransposeA {a: v},
    "PanM" <v:Value> => Op::PanM {m: v},
    "PanA" <v:Value> => Op::PanA {a: v},
    "Length" <v:Value> => Op::Length {m: v},
    "Gain" <v:Value> => Op::Gain {m: v},
    "Sequence" "[" <operations: Comma<Operation>> "]" => Op::Sequence { operations: operations },
    "Overlay" "[" <operations: Comma<Operation>> "]" => Op::Overlay { operations: operations }
};

Value: f32 = {
    <n:Num> => n,
    <n:Num> "/" <d:Num> => n/d

};

Num: f32 = <s:r"-?(0|([1-9]\d*))(\.\d+)?+"> => f32::from_str(s).unwrap();


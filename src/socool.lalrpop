use std::str::FromStr;
use std::string::String;
use crate::ast::{Init, Op};
use std::collections::HashMap;

grammar<'table>(table: &'table mut HashMap<String, Op>);

// pub SoCool: () = {
  //  <l: LetDefs> => ()
//}

pub SoCool: Init = {
  <init: Point> <l: LetDefs> => { init }
}

Point: Init = {
    "{"
         "f:" <f:Rational> ","
         "l:" <l:Rational> ","
         "g:" <g:Rational> ","
         "p:" <p:Rational>
    "}" ";"? => Init { f, l, g, p }
}

LetDefs = Break<LetDef>;
LetDef: () = {
    "let" <s: Name> "=" <o: Operation> => { table.insert(s.clone(), o); () },
}

Operation: Op = {
    <ops: Pipe<BaseOperation>> => Op::Compose { operations: ops },
    <o: BaseOperation> => o,
}

BaseOperation: Op = {
    "AsIs" => Op::AsIs,
    "Reverse" => Op::Reverse,
    //"Repeat" <i:Int> =>
    "Silence" <v:Rational> => Op::Silence {m: v},
    "Tm" <v:Rational> => Op::TransposeM {m: v},
    "Ta" <v:Rational> => Op::TransposeA {a: v},
    "PanM" <v:Rational> => Op::PanM {m: v},
    "PanA" <v:Rational> => Op::PanA {a: v},
    "Length" <v:Rational> => Op::Length {m: v},
    "Gain" <v:Rational> => Op::Gain {m: v},
    "Sequence" "[" <operations: Operations> "]" => Op::Sequence { operations: operations },
    "Overlay" "[" <operations: Operations> "]" => Op::Overlay { operations: operations },
    "o" "[" <o: Overtones> "]" => Op::Overlay { operations: o },
    "(" <o: Operation> ")" => o,
    <id: Name> => { table.get(&id).unwrap().clone() },
};

Overtone: Op = {
    "("
        <v:Rational> ","
        <o:Rational> ","
        <g:Rational> ","
        <p:Rational>
    ")"
    => Op::Compose { operations: vec! [
            Op::TransposeM { m: v },
            Op::TransposeA { a: o },
            Op::Gain { m: g },
            Op::PanA { a: p },
        ]
    }
}

Overtones = Comma<Overtone>;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Break<T>: Vec<T> = {
  <v:(<T> ";")*> => v
};

Pipe<T>: Vec<T> = {
    <v:(<T> "|")+> <e:T?> => match e {
        None=> v,
        Some(e) => {
          let mut v = v;
          v.push(e);
          v
        }
    }
};

Compose = Pipe<Operation>;

Operations = Comma<Operation>;

Num: f32 = {
   <n: Float> => n,
   <n: Int> => { n as f32 }
}

Rational: f32 = {
    <n:Num> => n,
    <n:Num> "/" <d:Num> => n/d
};

Name: String = <s:r"[a-z_$][a-zA-Z_$0-9]*"> => s.to_string();
Float: f32 = <s:r"-?(0|([1-9]\d*))\.\d+"> => f32::from_str(s).unwrap();
Int: u32 = <s:r"[0-9]+"> => u32::from_str(s).unwrap();

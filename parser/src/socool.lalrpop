use num_rational::{Rational64};
use std::str::FromStr;
use std::string::String;
use weresocool_ast::{
        Defs,
        Op,
        Op::*,
        Term, 
        ASR,
        FunDef,
        ListOp,
        Coefs,
        Axis,
        Generator,
        GenOp,
        Indices, 
        Index,
};
use crate::parser::Init;
use crate::indices::{et, random_seed};
use crate::float_to_rational::helpers::*;

grammar<'defs, 'err>(
    defs: &'defs mut Defs,
);

pub SoCool: Init = {
  <init: Point>
  <l: LetDefs> => init 
}

Point: Init = {
    "{"
         <f_axis: F> <f: Rational> ","
         <l_axis: L> <l: Rational> ","
         <g_axis: G> <g: Rational> ","
         <p_axis: P> <p: Rational>
    "}" ";"? => Init { f, l, g, p }
}

LetDefs = Lets<LetDef>;
LetDef: () = {
    <s: Name> "(" <vars: Comma<Name>>  ")" "=" "{" <o: Operation> "}" => {
         defs.terms.insert(
             s.clone(),
             Term::FunDef(FunDef {
                 name: s,
                 vars: vars,
                 term: Box::new(o)
             })
         );
        ()
    },

    <s: Name> "=" "{" <o: Operation> "}" => {
        defs.terms.insert(s.clone(), o); ()
    },

    <name: Name> "=" <list: List> => {
        defs.lists.insert(name.clone(), Term::Lop(list)); ()
    },
    
    <name: Name> "=" <generator: Generator> => {
        defs.generators.insert(name.clone(), Term::Gen(generator)); ()
    }
};


Operation: Term = {
    <op1: Operation> ">" "FitLength" <op2: SuperCompose> => {
        Term::Op(
            Op::Compose { operations:
            vec![op1.clone(), Term::Op(WithLengthRatioOf { with_length_of: Box::new(op2), main: Box::new(op1) })]
        })
    },

    <op1: Operation> ">" "FitGain" <op2: ComposeOperation> => {
      unimplemented!()
    },

    <op1: Operation> ">" "@" <name: Name> <op2: ComposeOperation> => {
        Term::Op(
            Focus {
                name: name,
                main: Box::new(op1.clone()),
                op_to_apply: Box::new(op2.clone()),
            }
        )
    },

    <op: ComposeOperation> => op,

    <list: List> => Term::Lop(list),

    <generator: Generator> => Term::Gen(generator)
}

Generator: GenOp = {
  <gen: GeneratorBase> "Take" <n: Int> => {
    GenOp::Taken {n: n as usize, gen: Box::new(gen)}
  },
  <gen: GeneratorBase> => gen

}

GeneratorBase: GenOp = {
  r"(Gen|\*)" "(" <coefs: Comma<Coefs>> ")" =>  {
    GenOp::Const(Generator {
      coefs,
    })
  },

  r"(Gen|\*)" <name: Name> => {
    GenOp::Named(name)
  },
}


Coefs: Coefs = {
  <axis: Axis> <div: ExplicitRational> "," "[" <coefs: Comma<Int>> "]" => { 
    Coefs {
      state: div.0,
      state_bak: div.0,
      div: div.1.abs() as usize,
      idx: 0,
      coefs,
      axis
    }
  }
}

List: ListOp = {
  <lists: ListConcat> => ListOp::Concat(lists),

  <list: ListIndexed> => list,

}

ListConcat = Concat<ListIndexed>;

ListIndexed: ListOp = {
  <listop: ListIndexed> "@" "[" <indices: Indices> "]" => {
    ListOp::ListOpIndexed { list_op: Box::new(listop), indices}
  },

   "<" <list: List> ">" => list,

  <list: ListBase> => list,
}

ListBase: ListOp = {
  r"(List|&)" "[" <operations: Operations> "]" => {
    ListOp::Const(operations)
  },

  r"(List|&)" <name: Name> => {
    ListOp::Named(name)
  },

  "ET" "(" <n: Int> ")" => {
    ListOp::Const(et(n))
  },

  r"(List|&)" <gen: Generator> => ListOp::GenOp(gen)
}

Indices: Indices = {
  <indices: Comma<Index>> => Indices(indices),
};

Index: Index = {
  <index: Int> => Index::Const { indices: vec![index] },
  "(" <indices: Comma<Int>> ")" => Index::Const { indices },
  <start: Int?>":"<end: Int?><skip: (":" Int)?> => { 
      match skip {
        Some(skip) => Index::Slice { start, end, skip: skip.1 },
        None => Index::Slice { start, end, skip: 1 }
      }
  },
  "Random" "(" <n: Int> <seed: ("," Int)?> ")" => {
      match seed {
        Some(seed) => {
          Index::Random { n, seed: seed.1 }
        },
        None => {
          Index::Random { n, seed: random_seed() }
        }
      }
  },
  <index: Index> "|" <term: Composeable> => Index::IndexAndTerm {index: Box::new(index), term},
} 

SuperCompose: Term = {
  <o: ComposeOperation> => o,
  <o: Generator> => Term::Gen(o)
}

ComposeOperation: Term = {
    <ops: Pipe<Composeable>> => Term::Op(Compose { operations: ops }),
    <o: BaseOperation> => o,
}

Composeable: Term = {
  <base_operation: BaseOperation> => base_operation,
  <list: List> => Term::Lop(list),
  <generator: Generator> => Term::Gen(generator)
}

BaseOperation: Term = {
    "(" <o: Operation> ")" => o,
    <fm: Fm> <m:Rational> => Term::Op(TransposeM {m}),
    <fa: Fa> <a:Rational> => Term::Op(TransposeA {a}),
    <pm: Pm>  <m:Rational> => Term::Op(PanM {m}),
    <pa: Pa>   <a:Rational> => Term::Op(PanA {a}),
    <lm: Lm> <m:Rational> => Term::Op(Length {m}),
    <gain: Gm> <m:Rational> => Term::Op(Gain {m}),
    "AsIs" => Term::Op(AsIs {}),
    "Sine" <pow: Rational?> => {
      Term::Op(Sine {pow})
    },
    "Square" => Term::Op(Square {}),
    "Noise" => Term::Op(Noise {}),
    "Reverse" => Term::Op(Reverse {}),
    "Invert" => Term::Op(FInvert),
    "Repeat" <i: Int> => {
        let mut vec = Vec::new();
        for x in 0..i {
            vec.push(Term::Op(AsIs))
        }
        Term::Op(Sequence { operations: vec })
    },
    "AD" "("
        <attack:Rational> ","
        <decay: Rational> ","
        <decay_type: Int> ")" => { 
          let asr = if decay_type == 2 {ASR::Long} else {ASR::Short};
          Term::Op(AD {attack, decay, asr})
        },
    "Portamento" <m:Rational> => Term::Op(Portamento {m}),
    "Reverb" <m:Rational> => Term::Op(Reverb {m}),
    "Silence" <m:Rational> => Term::Op(Silence {m}),
    r"(Sequence|Seq)" "[" <operations: Operations> "]" => Term::Op(Sequence { operations: operations }),
    "Overlay" "[" <operations: Operations> "]" => Term::Op(Overlay { operations: operations }),
    r"(ModulateBy|ModBy)" "[" <operations: Operations> "]" => Term::Op(ModulateBy { operations: operations }),
    "O" "[" <o: Overtones> "]" => Term::Op(Overlay { operations: o }),
    <id: Name> => {
        Term::Op(Id(id))
    },
    "#"<name: Name> => Term::Op(Tag(name)),
    //
    <name: Name> "(" <args: Comma<Operation>>  ")" => {
        Term::Op(
            FunctionCall {
                name,
                args
            }
        )
    },
    <tm: Rational>"^"<length: Rational> => {
        Term::Op(Compose { operations: vec! [
            Term::Op(TransposeM { m: tm }),
            Term::Op(Length { m: length }),
        ]})
    },
};


Overtone: Term = {
    "("
        <fm:Rational> ","
        <fa:Rational> ","
        <g:Rational> ","
        <p:Rational>
    ")"
    => Term::Op(Compose { operations: vec! [
            Term::Op(TransposeM { m: fm }),
            Term::Op(TransposeA { a: fa }),
            Term::Op(Gain { m: g }),
            Term::Op(PanA { a: p }),
        ]
    })
}


Overtones = Comma<Overtone>;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Import: Vec<String> = {
    <a: Name>"."<b: Name> => vec![a, b],
    <a: Name> => vec![a]
}

Lets<T>: Vec<T> = {
  <v:(<T>)*> => v
};

Pipe<T>: Vec<T> = {
    <v:(<T> "|")+> <e:T?> => match e {
        None=> v,
        Some(e) => {
          let mut v = v;
          v.push(e);
          v
        }
    }
};

Concat<T>: Vec<T> = {
    <v:(<T> "++")+> <e:T?> => match e {
        None=> v,
        Some(e) => {
          let mut v = v;
          v.push(e);
          v
        }
    }
};

Compose = Pipe<Operation>;

Operations = Comma<Operation>;

Ints = Comma<Int>;

Rational: Rational64 = {
    <n: Float> => f32_to_rational(n),
    <n: Int> => Rational64::new(n, 1),
    <r: ExplicitRational> => Rational64::new(r.0, r.1)
};

ExplicitRational: (i64, i64) = {
    <n: Int> "/" <d:Int> => (n, d),
}

Axis: Axis = {
    F => Axis::F,
    G => Axis::G,
    L => Axis::L,
    P => Axis::P,
};

Fm: String = <s: r"Fm|Tm"> => s.to_string();
Fa: String = <s: r"Fa|Ta"> => s.to_string();
Gm: String = <s: r"Gain|Gm"> => s.to_string();
Lm: String = <s: r"Length|Lm"> => s.to_string();
Pm: String = <s: r"PanM|Pm"> => s.to_string();
Pa: String = <s: r"PanA|Pa"> => s.to_string();

F: String = <s: r"f:"> => s.to_string();
G: String = <s: r"g:"> => s.to_string();
L: String = <s: r"l:"> => s.to_string();
P: String = <s: r"p:"> => s.to_string();


Name: String = <s:r"[a-z_][a-zA-Z_0-9.]*"> => s.to_string();
Float: String = <s:r"-?(0|([1-9]\d*))\.\d+"> => s.to_string();
Int: i64 = <s:r"-?[0-9]+"> => i64::from_str(s).unwrap();
